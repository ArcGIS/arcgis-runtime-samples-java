plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

group = 'com.esri.samples'

ext {
    arcgisVersion = '200.0.0-beta+1'
}

javafx {
    version = "17.0.2"
    modules = [ 'javafx.base', 'javafx.controls', 'javafx.media', 'javafx.web', 'javafx.fxml', 'javafx.graphics' ]
}

application {
    mainModule = "edit.and.sync.features.main"
    mainClass = "com.esri.samples.edit_and_sync_features.EditAndSyncFeaturesSample"
}

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven {
        url 'https://esri.jfrog.io/artifactory/arcgis'
    }
    maven {
        url 'https://olympus.esri.com/artifactory/arcgisruntime-repo'
    }
}

configurations {
    natives
}

dependencies {
    implementation "com.esri.arcgisruntime:arcgis-java:$arcgisVersion"
    natives "com.esri.arcgisruntime:arcgis-java-jnilibs:$arcgisVersion"
    natives "com.esri.arcgisruntime:arcgis-java-resources:$arcgisVersion"
    // handle SLF4J http://www.slf4j.org/codes.html#StaticLoggerBinder
    runtimeOnly 'org.slf4j:slf4j-nop:1.7.32'
}

task copyNatives(type: Copy) {
    description = "Copies the arcgis native libraries into the project build directory for development."
    group = "build"
    configurations.natives.asFileTree.each {
        from(zipTree(it))
    }
    // store native libraries in a common location shared with other samples
    into "${System.properties.getProperty("user.home")}/.arcgis/$arcgisVersion"
}

run {
    dependsOn copyNatives
}

task productionZip(type: Zip) {
    group = 'distribution'
    from ("samples-data") {
        into "samples-data"
    }
    from copyNatives
    from jar.destinationDirectory
    into (project.name)
    archiveBaseName = project.name
}

wrapper {
    gradleVersion = '7.5.1'
}
