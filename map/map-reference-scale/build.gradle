plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' // version is defined in settings.gradle
}

group = 'com.esri.samples'

ext {
    arcgisVersion = '200.2.0'
}

javafx {
    version = "17.0.9"
    modules = [ 'javafx.controls', 'javafx.graphics', 'javafx.fxml', 'javafx.web', 'javafx.media' ]
}

application {
    mainModule = "com.esri.samples.map_reference_scale"
    mainClass = "com.esri.samples.map_reference_scale.MapReferenceScaleSample"
}

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven {
        url 'https://esri.jfrog.io/artifactory/arcgis'
    }
    maven {
        url 'https://olympus.esri.com/artifactory/arcgisruntime-repo'
    }
}

configurations {
    natives
}

dependencies {
    implementation "com.esri.arcgisruntime:arcgis-java:$arcgisVersion"
    natives "com.esri.arcgisruntime:arcgis-java-jnilibs:$arcgisVersion"
    natives "com.esri.arcgisruntime:arcgis-java-resources:$arcgisVersion"
    // handle SLF4J http://www.slf4j.org/codes.html#StaticLoggerBinder
    implementation 'org.slf4j:slf4j-nop:2.0.9'
}

task copyNatives(type: Copy) {
    description = "Copies the arcgis native libraries into the project build directory for development."
    group = "build"
    configurations.natives.asFileTree.each {
        from(zipTree(it))
    }
    // store native libraries in a common location shared with other samples
    into "${System.properties.getProperty("user.home")}/.arcgis/$arcgisVersion"
}

run {
    dependsOn copyNatives
}

task productionZip(type: Zip) {
    group = 'distribution'
    from copyNatives
    from jar.destinationDirectory
    into (project.name)
    archiveBaseName = project.name
}

if (rootProject == project) {
    // the wrapper task is only used if this project is the root project i.e. not a subproject
    wrapper {
        gradleVersion = '8.1.1'
    }
}
